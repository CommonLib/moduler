apply plugin: 'com.android.application'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion
    defaultConfig {
        applicationId "com.architecture.extend.architecture"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [moduleName: project.getName()]
            }
        }

    }

    signingConfigs {
        config {
            storeFile file(rootProject.ext.keyPath)
            storePassword rootProject.ext.storePassword
            keyAlias rootProject.ext.keyAlias
            keyPassword rootProject.ext.keyPassword
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }
        debug {
            signingConfig signingConfigs.config
        }
    }

    dataBinding {
        enabled = true
    }

    android.applicationVariants.all { variant ->
        variant.outputs.all {
            if (variant.buildType.name == 'release') {
                outputFileName = "app_${defaultConfig.versionName}_${releaseTime()}_release.apk"
            } else if (variant.buildType.name == 'debug') {
                outputFileName = "app_${defaultConfig.versionName}_${releaseTime()}_debug.apk"
            }
        }
    }
}

static def releaseTime() {
    return new Date().format("yyyy-MM-dd", TimeZone.getTimeZone("UTC"))
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    annotationProcessor 'com.alibaba:arouter-compiler:1.1.4'
    annotationProcessor 'dagger.object.inject:dagger-object-processor:2.13'
    annotationProcessor "android.arch.persistence.room:compiler:1.1.0"
    annotationProcessor 'com.google.dagger:dagger-compiler:2.13'
    if (Boolean.parseBoolean(buildAll)) {
        compile project(':p_web')
        compile project(':p_pic')
    } else {
        compile project(':contract')
    }
}
